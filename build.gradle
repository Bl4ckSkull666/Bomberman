apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'idea'

sourceCompatibility = 1.8

group = 'io.github.mdsimmo'
version = '0.4.0'

repositories {
    mavenCentral()
    jcenter()
    maven {
        // bukkits repo
        url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        // vaults repo
        url "https://jitpack.io"
    }
}

dependencies {
    compileOnly "org.bukkit:bukkit:1.14.4-R0.1-SNAPSHOT"
    testCompile "org.bukkit:bukkit:1.14.4-R0.1-SNAPSHOT"
    
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"
    compile "net.objecthunter:exp4j:0.4.8"
    
    testCompile "junit:junit:4.+"
    testCompile "org.mockito:mockito-core:1.+"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

processResources  {
    // auto assign values in the plugin.yml
    filter {
        line ->
            line
                .replace('${name}', project.name)
                .replace('${version}', version)
    }
}

task clearOldPluginJar( type: Delete ) {
    description 'Deletes any old bomberman plugins from the server'
    if ( project.hasProperty("serverLocation" ) ) {
        delete project.fileTree( "$serverLocation/plugins" ) {
            include '*omberman*.jar'
        }
    }
}

task copyToServer(type: Copy, dependsOn: clearOldPluginJar ) {
    description 'Copies the plugin to the server'
    if ( project.hasProperty("serverLocation" ) ) {
        from jar
        into "$serverLocation/plugins"
    }
}

task install( dependsOn: [build, copyToServer] ) {
    description 'Compiles, tests and copies the code to the server'
}

jar {
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}
buildscript {
    ext.kotlin_version = '1.3.50'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
